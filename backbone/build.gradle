apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

version = '2.2.4'

android {
    compileSdkVersion 30

    dexOptions {
        preDexLibraries = false
    }

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 30
        versionCode 117
        versionName version
        vectorDrawables.useSupportLibrary = true

        multiDexEnabled true
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    lintOptions {
        abortOnError false
    }

    resourcePrefix 'rsb_'
}

// Reading in data from local.properties is used here to grab key/value pairs used below in ext
// Originally, it read the local.properties, but this file should never be committed to vcs,
// So check if it exists first, because some projects may not care about it
Properties properties = new Properties()
if (project.rootProject.file('local.properties').exists()) {
    properties.load(project.rootProject.file('local.properties').newDataInputStream())
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // These are all support libraries that should be updated when Google releases new ones
    api 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    api 'androidx.legacy:legacy-preference-v14:1.0.0'
    implementation 'androidx.annotation:annotation:1.2.0'
    api 'com.google.android.material:material:1.4.0'
    api 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'androidx.multidex:multidex:2.0.1'

    implementation 'io.reactivex:rxjava:1.3.0'
    implementation 'nl.littlerobots.rxlint:rxlint:1.6.1'
    api 'com.google.code.gson:gson:2.8.6'

    implementation 'com.cronutils:cron-utils:3.1.2', {
        exclude group: 'joda-time', module: 'joda-time' // this can cause dex errors due to duplicate joda times
    }
    api 'net.danlew:android.joda:2.9.9.4' // android specific joda time, jre version causes perf problems
    testImplementation 'joda-time:joda-time:2.9.9'

    api 'com.squareup.retrofit2:retrofit:2.4.0'
    api 'com.squareup.retrofit2:converter-gson:2.3.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    api 'com.squareup.okhttp3:logging-interceptor:3.9.1'

    api 'io.reactivex:rxandroid:1.2.1'

    api 'com.jakewharton.rxbinding:rxbinding:0.4.0'
    api 'com.jakewharton.rxbinding:rxbinding-support-v4:0.4.0'
    api 'com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.4.0'
    api 'com.jakewharton.rxbinding:rxbinding-design:0.4.0'

    // Used to display UploadData and study data in various chart formats
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.3'

    api 'com.scottyab:aes-crypto:0.0.3'
    api 'co.touchlab.squeaky:squeaky-query:0.4.0.0'
    annotationProcessor 'co.touchlab.squeaky:squeaky-processor:0.4.0.0'
    api 'net.zetetic:android-database-sqlcipher:4.0.1@aar'

    // Libraries to help with unit testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-core:3.4.4'
    androidTestImplementation 'org.mockito:mockito-android:3.4.4'
    testImplementation "org.powermock:powermock-module-junit4:2.0.4"
    testImplementation "org.powermock:powermock-module-junit4-rule:2.0.4"
    testImplementation "org.powermock:powermock-api-mockito2:2.0.4"
    testImplementation "org.powermock:powermock-classloading-xstream:2.0.4"

    testImplementation 'org.robolectric:robolectric:3.5'
}


task sourceJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier "source"
}
project.afterEvaluate {
    publishing {
        publications {
            msdk(MavenPublication) {

                groupId 'com.medaptivehealth.researchstack'
                artifactId 'backbone'
                version version
                artifact(bundleReleaseAar)
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')
                    configurations.api.allDependencies.each {
                        if (it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null) {
                            def dependencyNode = dependenciesNode.appendNode('dependency')
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                        }
                    }
                }
            }
        }
        repositories {
            maven {
                // change to point to your repo, e.g. http://my.org/repo
                url "${System.env.HOME}/.m2/repository"
            }
        }
    }
}
